import estudiante.*
import testHelper.*
import actividades.*

describe "Estudiante" {
	test "su nivel de concentracion cuando no esta estresado es su energia" {
		const estudianteDesestresado = new Estudiante(energia=30, estresado=false)
		
		assert.equals(30, estudianteDesestresado.concentracion())
	}

	test "su nivel de concentracion cuando esta estresado es la mitad de su energia" {
		const estudianteEstresado = new Estudiante(energia=30, estresado=true)
		
		assert.equals(15, estudianteEstresado.concentracion())
	}
	
	test "esta concentrado si su nivel de concentracion es mayor a 50" {
		const estudianteConcentrado = new Estudiante(energia=51, estresado=false)

		assert.that(estudianteConcentrado.concentrado())
	}
	
	test "no esta concentrado si su nivel de concentracion es igual a 50" {
		const estudianteConcentrado = new Estudiante(energia=50, estresado=false)

		assert.notThat(estudianteConcentrado.concentrado())
	}
	
	test "no esta concentrado si su nivel de concentracion es menor a 50" {
		const estudianteDesconcentrado = new Estudiante(energia=49, estresado=false)

		assert.notThat(estudianteDesconcentrado.concentrado())
	}
	
	test "no esta preparado si estudio menos de 40 horas" {
		const estudianteNoPreparado = testHelper.estudianteConcentradoConHorasDeEstudio(39)
		
		assert.notThat(estudianteNoPreparado.preparado())
	}
	
	test "no esta preparado si estudio 40 horas" {
		const estudiantePreparado = testHelper.estudianteConcentradoConHorasDeEstudio(40)
		
		assert.notThat(estudiantePreparado.preparado())
	}
	
	test "no esta preparado si no esta concentrado" {
		const estudianteNoPreparado = testHelper.estudianteDesconcentrado()
		
		assert.notThat(estudianteNoPreparado.preparado())
	}
	
	test "esta preparado si esta concentrado y estudio mas de 40 horas" {
		const estudiantePreparado = testHelper.estudianteConcentradoConHorasDeEstudio(41)
		
		assert.that(estudiantePreparado.preparado())
	}
	
	test "si su rutina de estudio esta vac√≠a hacerla lo deja igual que como estaba" {
		const estudiante = new Estudiante(energia=0, estresado=false, horasDeEstudio=0,
										  rutinaDeEstudio=[])

		estudiante.hacerRutinaDeEstudio()
		
		assert.equals(0, estudiante.energia())
		assert.notThat(estudiante.estresado())
		assert.equals(0, estudiante.horasDeEstudio())
	}
	
	test "si su rutina de estudio tiene tareas que no puede hacer lo deja igual que como estaba" {
		const estudiante = new Estudiante(energia=0, estresado=false, horasDeEstudio=0,
										  rutinaDeEstudio=[estudiar.temaDificil(5)])

		estudiante.hacerRutinaDeEstudio()

		assert.equals(0, estudiante.energia())
		assert.notThat(estudiante.estresado())
		assert.equals(0, estudiante.horasDeEstudio())
	}
	
	test "hace su una rutina de estudio, haciendo una a una aquellas actividades que pueda hacer
			y salteandose las que no" {
		const tareaQueNoPuedeHacerPorqueAunNoEstaConcentrado = estudiar.temaDificil(5)
		const estudiante = new Estudiante(energia=0, estresado=false, horasDeEstudio=0,
										  rutinaDeEstudio=[tareaQueNoPuedeHacerPorqueAunNoEstaConcentrado,
										  				   new TomarCafe(cafeina=250),
										  				   new TomarCafe(cafeina=250),
										  				   new EscucharMusica(),
										  				   estudiar.temaDificil(5)])

		estudiante.hacerRutinaDeEstudio()

		assert.equals(10, estudiante.energia())
		assert.notThat(estudiante.estresado())
		assert.equals(5, estudiante.horasDeEstudio())
	}
}
